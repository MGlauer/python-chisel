%import common.WS
%import common.SIGNED_NUMBER
%import common.NUMBER
%ignore WS

DOLLAR_WORD: "$" WORD
WORD: /\w+/
UPPER_WORD: ("A" .. "Z") WORD?
LOWER_WORD: LOWER WORD?
LOWER: "a" .. "z"

defined_type: DOLLAR_WORD
type: WORD

type_expression: ( defined_type | type) [ "(" type_expression ("," type_expression )* ")" ]

variable:  UPPER_WORD
constant: LOWER_WORD

typed_constant: constant ":" type_expression
typed_variable: constant ":" type_expression


DECIMAL_NUMBER: SIGNED_NUMBER ("/" NUMBER)?

distinct_object: /"([^"]|((?<=\\)"))+"/
?single_quoted:  /'([^']|((?<=\\)'))+'/
?any_word: single_quoted | WORD | DOLLAR_WORD
?functor: LOWER_WORD | DOLLAR_WORD | single_quoted


OBJECT_BINARY_OPERATOR: "=" | "!="
TYPE_BINARY_OPERATOR: "@" | "*" | "+" | "-->" | ":=" | ">"
LOGIC_BINARY_OPERATOR: "=>" | "<=>" | "<=" | "<~>" | "~&" | "~|"

UNARY_CONNECTIVE: "~"
QUANTIFIER: "!" | "?"

?formula: disjunction

?disjunction: conjunction ("|" conjunction)*
?conjunction: logic_binary_formula ("&" logic_binary_formula)*
?logic_binary_formula: type_binary_formula (LOGIC_BINARY_OPERATOR type_binary_formula)*
?type_binary_formula: unary_formula (TYPE_BINARY_OPERATOR unary_formula)*
?unary_formula: (UNARY_CONNECTIVE unary_formula) | transition_formula | typed_constant
?transition_formula: object_binary_formula | "(" formula ")" | quantified_formula
quantified_formula: QUANTIFIER "[" (variable ("," variable)*) "]" ":" unary_formula
?object_binary_formula: term (OBJECT_BINARY_OPERATOR term)*

?term: functor_term | bottom | DECIMAL_NUMBER | distinct_object
functor_term: functor ["(" term ("," term)* ")"]
?bottom: variable // | "(" term ")"

LOGIC: "thf" | "tff" | "cnf" | "fof"

inference_rule: WORD "(" WORD ("," WORD )* ")"

inference_item: WORD | inference_rule

intro_type: "definition" | "axiom_of_choice" | "tautology" | "assumption"

//inference_source: "inference(" WORD "," "[" inference_item ("," inference_item )*  "]"  "," "[" source "]" ")"
//internal_source: "introduced(" WORD ( "," sources )? ")"
//any_word ( "(" source ("," source )* ")" )? | ((any_word ":")?)
//sources: "["  source ("," source)* "]"
// ?source: file_source // | inference_source | internal_source |  sources | /(?!(inference|introduced|file))\w+/

file_source: "file(" single_quoted [ "," WORD ] ")"
generic_annotation: /(?!\s*file)(([^)]|\)(?!\.))+)/sx
?source: file_source

annotation: ( source ["," generic_annotation ] | generic_annotation )

FORMULA_ROLE: "axiom" | "hypothesis" | "definition" | "assumption" | "lemma" | "theorem" | "corollary" | "conjecture" | "negated_conjecture" | "plain" | "type" | "fi_domain" | "fi_functors" | "fi_predicates" | "unknown"

annotated_formula: LOGIC "(" ( WORD | single_quoted ) "," FORMULA_ROLE "," formula [ "," annotation ] ")."
include: "include(" "'" /[^']+/ "'" ( "," "[" WORD ("," WORD)* "]" )? ")."

%ignore COMMENT
%ignore BLOCK_COMMENT

COMMENT: /%[^\n]*\n/
BLOCK_COMMENT: /\/\*([^*]|\*(?!\/))*\*\//m
?tptp_line: annotated_formula | include | COMMENT | BLOCK_COMMENT

start: tptp_line+
